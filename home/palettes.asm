RestoreScreenTilesAndReloadTilePatterns::
	call ClearSprites
	ld a, $1
	ld [wUpdateSpritesEnabled], a
	call ReloadMapSpriteTilePatterns
	call LoadScreenTilesFromBuffer2
;	call LoadTextBoxTilePatterns ; marcelnote - this restore path no longer clobbers textbox tiles ($79-$7f)
	call RunDefaultPaletteCommand
	jr Delay3

GBPalWhiteOutWithDelay3::
	call GBPalWhiteOut
	; fallthrough

Delay3::
; The bg map is updated each frame in thirds.
; Wait three frames to let the bg map fully update.
	ld c, 3
	jp DelayFrames

GBPalNormal::
; Reset BGP and OBP0.
	ld a, %11100100 ; 3210
	ldh [rBGP], a
	ld a, %11010000 ; 3100
	ldh [rOBP0], a
	ret

GBPalWhiteOut::
; White out all palettes.
	xor a
	ldh [rBGP], a
	ldh [rOBP0], a
	ldh [rOBP1], a
	ret

; RunDefaultPaletteCommand::
; 	ld b, SET_PAL_DEFAULT
; RunPaletteCommand::
; 	ld a, [wOnSGB]
; 	and a
; 	ret z
; 	predef_jump _RunPaletteCommand

RunDefaultPaletteCommand::
	ld b, SET_PAL_DEFAULT

RunPaletteCommand::
IF DEF(_ATOZUKE_GBC)
	predef_jump _RunPaletteCommand
ELSE
	ld a, [wOnSGB]
	and a
	ret z
	predef_jump _RunPaletteCommand
ENDC

GetHealthBarColor::
; Return at hl the palette of
; an HP bar e pixels long.
	ld a, e
	cp 27
	ld d, 0 ; HP_BAR_GREEN
	jr nc, .gotColor
	cp 10
	inc d   ; HP_BAR_YELLOW
	jr nc, .gotColor
	inc d   ; HP_BAR_RED
.gotColor
	ld [hl], d
	ret
